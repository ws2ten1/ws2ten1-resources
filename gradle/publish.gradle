publishing {
	publications {
		mavenJava(MavenPublication) {
			versionMapping {
				usage('java-api') {
					fromResolutionOf('runtimeClasspath')
				}
				usage('java-runtime') {
					fromResolutionResult()
				}
			}
			from components.java
			artifact sourcesJar {
				classifier 'sources'
			}
			artifact javadocJar {
				classifier 'javadoc'
			}
			pom.withXml {
				asNode().children().last() + {
					resolveStrategy = Closure.DELEGATE_FIRST
					name project.name
					description project.description
					inceptionYear '2020'
					licenses {
						license {
							name 'The Apache Software License, Version 2.0'
							url 'http://www.apache.org/license/LICENSE-2.0.txt'
							distribution 'repo'
						}
					}
				}
			}
		}
	}
}

if (System.env.BINTRAY_USER && System.env.BINTRAY_KEY) {
	bintray {
		user = System.env.BINTRAY_USER
		key = System.env.BINTRAY_KEY
		publications = ['mavenJava']
		dryRun = System.env.CIRCLE_TAG == null
		publish = true
		pkg {
			userOrg = 'polycreo'
			repo = 'maven'
			name = project.name
			desc = project.description
			websiteUrl = 'https://github.com/polycreo/polycreo-resources'
			issueTrackerUrl = 'https://github.com/polycreo/polycreo-resources/issues'
			vcsUrl = 'https://github.com/polycreo/polycreo-resources.git'
			licenses = ['Apache-2.0']
			labels = ['polycreo', 'Spring framework']
			githubRepo = 'polycreo/polycreo-resources'
			version {
				name = project.version
				vcsTag = project.version
			}
		}
	}
}
